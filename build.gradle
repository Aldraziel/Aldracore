plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.aldraziel'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    dependencies {
        api 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
        api 'org.jetbrains:annotations:23.0.0'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username System.getenv('GITHUB_USERNAME')
                    password System.getenv('GITHUB_TOKEN')
                }
                name = "Aldracore"
                url = "https://maven.pkg.github.com/Aldraziel/Aldracore"
            }
        }
    }
}
version '1.0'

shadowJar {
    configurations = [project.configurations.shadow]
}

dependencies {
    shadow 'redis.clients:jedis:4.0.1'
    shadow project(':API')
}
