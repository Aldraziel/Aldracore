plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.aldraziel'
    version '1.0.3'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    dependencies {
        api 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
        api 'org.jetbrains:annotations:23.0.0'
    }

    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
        from javadoc
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username System.getenv('USERNAME')
                    password System.getenv('TOKEN')
                }
                name = "Aldracore_maven"
                url = "https://maven.pkg.github.com/Aldraziel/Aldracore"
            }
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'de.tr7zw.changeme.nbtapi', 'fr.aldraziel.nbtapi'
}

repositories {
    maven {
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        credentials {
            username System.getenv('USERNAME')
            password System.getenv('TOKEN')
        }
        name "AldraRedis_maven"
        url "https://maven.pkg.github.com/Aldraziel/AldraRedis"
    }
}

dependencies {
    implementation 'fr.flowarg.aldraziel:aldraredis:1.0.1-SNAPSHOT'
    shadow 'de.tr7zw:item-nbt-api:2.11.3'
    shadow project(':API')
}